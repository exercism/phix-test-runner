requires("1.0.5") -- (assert(a&b&c) bug, -nopause)
without trace
include builtins/json.e 

procedure process()
    sequence cl = command_line()
    integer cll = length(cl)
    if cll<4 then
        puts(1, "usage: p ./bin/run.exw slug input_dir output_dir\n")
        return
    end if
    string exercise_folder = cl[4],             -- eg exercism/phix/exercises/practice/acronym
           slug = split_path(exercise_folder)[$],   -- eg acronym
           sol_dir = get_proper_path(exercise_folder),
           out_dir = get_proper_path(cl[5]),
           test_src = join_path({sol_dir, "test.exw"}),
           res_file = join_path({out_dir, "results.json"})

    if not file_exists(sol_dir) then
        crash("%s does not exist",{sol_dir})
    end if
    if not file_exists(out_dir) then
        assert(create_directory(out_dir),"cannot create directory "&out_dir)
    end if

--  printf(1, "%s: testing...\n", {slug})

    assert(chdir(sol_dir),"cannot chdir to %s",{sol_dir})
    string cmd = sprintf(`p -nopause %s > "%s" 2>&1`, {test_src, res_file})
--?cmd

    integer res = system_exec(cmd,4) -- (4 === result/wait and redirect/builtin)
--?res

    sequence lines = get_text(res_file,GT_LF_STRIPPED)
--?lines
    
    string status = "pass", message = ""

--printf(1,"DATA:\n%s\n",{join(lines,"<\n")})
    integer begins_hat = 0, begins_3 = 0, crashmsg = false
    for i,l in lines do
        if begins_hat=0
        and begins("^",trim_head(l)) then
            begins_hat = i
        elsif begins("... called from ",l)
          or begins("...included by ",l)
         or begins("Global & Local Variables",l) then
            begins_3 = i-1
            exit
        elsif match("passed: ",l) then
            lines[i] = ""
        elsif i=5 and begins("--> see ",l) then
            crashmsg = true
            exit
        end if
    end for
    if crashmsg then
        status = "error"
        message = lines[3]
    elsif begins_3
       or begins_hat then
        -- compilation error
        status = "error"
        if begins_3=0 then begins_3 = begins_hat end if
        -- We might want to limit this to, say, 8 lines?
        message = join(filter(lines[2..begins_3],"!=",""),'\n')
    else
        -- check for unit test errors or success
        integer first_failure = 0
        lines = filter(lines,"!=","")
        for i,l in lines do
            if match("100% success", l) then exit end if // all good
            if match("% success", l) then // < 100% then...
                status = "fail"
                if first_failure==0 then first_failure = i end if
                message = trim(lines[first_failure])
                exit
            end if
            if first_failure==0 and match("failed:", l) then
                first_failure = i
            end if
        end for
    end if
    // if status is still "pass" then res //must// be 0, shirley:
    assert(status!="pass" or res==0,"pass with res=%d??",{res})
    
    sequence json = {JSON_OBJECT, {"version", 1}, {"status", status}, {"message", message}}
    integer ofn = open(res_file,"w")
    object pjres = print_json(ofn, json) -- (separate line so available for debugging)
    assert(pjres!=0,"bad json??")
    close(ofn)
--temp:
--{} = print_json(1,json)
--?{res,status}
--  if res!=0 then crash(status) end if

end procedure

process()

